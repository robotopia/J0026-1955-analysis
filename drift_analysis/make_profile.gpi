reset

set terminal pngcairo size 800,800 font ',14'
set output "profiles.png"

mjds = "59094 59003 58991 58434"
filenames = "1283104232.json.profile 1275178816.json.profile 1274143152.json.profile 1226062160.json.profile"
null_filenames = "1283104232_N_profile.txt 1275178816_N_profile.txt 1274143152_N_profile.txt 1226062160_N_profile.txt"
npulses = "1374 914 914 3759"

set xrange [-59:62] noextend
set xlabel "Pulse phase (deg)"
set xtics nomirror out
unset ytics

colours = "black red #00cc00 blue"

# Set up binning for combined profile
binwidth = 360.0/1024.0
bin(val) = binwidth * floor(val/binwidth + 0.5)

set multiplot

set rmargin at screen 0.98
set lmargin at screen 0.02

do for [i=1:words(filenames)+1] {

set bmargin at screen 0.13+0.17*(i-1)
set tmargin at screen 0.13+0.17*i

unset ylabel

set border (i == 1 ? 1 : 0)

if (i < 5) {
    set label 1 at graph 0, graph 0.5 "MJD".word(mjds, i)." " left
    plot word(null_filenames, i) u 1:($2/word(npulses, i)) w l lc rgb "gray" lw 1.5 notitle, \
         word(filenames, i) u 1:2 w l lc rgb "black" lw 1.5 notitle, \
         0 lc rgb "black" lw 2 notitle
} else {
    set label 1 at graph 0, graph 0.5 "Combined" left
    plot "< cat ".null_filenames u (bin(column(1))):3 smooth frequency w l lc rgb "gray" lw 1.5 notitle, \
         "< cat ".filenames u (bin(column(1))):2 smooth frequency w l lc rgb "black" lw 1.5 notitle, \
         0 lc rgb "black" lw 2 notitle
}

# Turn off x-axis markings for the other plots
unset xlabel
unset xtics

}

unset multiplot
