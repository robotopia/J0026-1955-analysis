reset

P = 1.306150273284

obsids = "1283104232 1275178816 1275172216 1275094456 1274143152 1226062160"
obsid(i) = word(obsids, i)
N = words(obsids)

MJDs = "59094 59003 59002 59002 58991 58434"
MJD(i) = word(MJDs, i)

maxima_file(i) = sprintf("%s.json.maxima", obsid(i))
subpulses_file(i) = sprintf("%s.subpulses", obsid(i))

my_ytics(i) = 1.5*i - 0.1
my_ytic_label_pos(i) = my_ytics(i) + 0.6

set xtics tc rgb "white"
set ytics (0)
do for [i=1:N] {
    set ytics add (MJD(i) my_ytic_label_pos(i))
}
set ytics out nomirror tc rgb "white"

set xrange [0:82] noextend
set yrange [my_ytics(1)-0.1:my_ytics(N)+1.6]

set xlabel "Time (min)" tc rgb "white"
set ylabel "MJD / Peak flux (a.u.)" tc rgb "white"

set style fill solid
set boxwidth P/60.0/2

unset key

set multiplot

do for [i=N:1:-1] {
    plot subpulses_file(i) u ($5*P/60.0):(my_ytics(i)+1.5) with boxes fc "#cccccc"
    plot subpulses_file(i) u ($5*P/60.0):(my_ytics(i))     with boxes fc rgb "white"
}

# Set all the labels black again
set xtics tc rgb "black"
set ytics out nomirror tc rgb "black"
set xlabel "Time (min)" tc rgb "black"
set ylabel "MJD / Peak flux (a.u.)" tc rgb "black"
plot for [i=1:N] maxima_file(i) using ($1*P/60.0):($2 + my_ytics(i)) w l lc rgb "black"

unset multiplot
