reset

P = 1.306150273284

obsids = "1283104232 1275178816 1274143152 1226062160"
obsid(i) = word(obsids, i)
N = words(obsids)

MJDs = "59094 59003 58991 58434"
MJD(i) = word(MJDs, i)

maxima_file(i) = sprintf("%s.json.maxima", obsid(i))

my_ytics(i) = 1.5*i - 0.1
my_ytic_label_pos(i) = my_ytics(i) + 0.6

set ytics (0)
do for [i=1:N] {
    set ytics add (MJD(i) my_ytic_label_pos(i))
}
set ytics out nomirror

set xrange [*:*] noextend
set yrange [1.3:7.5]

set xlabel "Time (min)"
set ylabel "Peak flux (a.u.)"

set arrow 1 from 40,my_ytics(2) to 60,my_ytics(2) nohead lw 2 lc rgb "black"
set arrow 2 from 40,my_ytics(3) to 60,my_ytics(3) nohead lw 2 lc rgb "black"

plot for [i=1:N] maxima_file(i) using ($1*P/60.0):($2 + my_ytics(i)) w l lc rgb "black" notitle
